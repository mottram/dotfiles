# File: ~/.zshrc
# Version: 0.3
# Author: Jack Mottram <j@ck.mottr.am>

# General
PATH="/usr/local/bin:/usr/local/sbin:$HOME/bin:$PATH"
if [[ $IS_OSX -eq 1 ]]; then
    export EDITOR=/usr/local/bin/vim
elif [[ $IS_LINUX -eq 1 ]]; then
    export EDITOR=vim
fi

# Source checks, aliases, functions and plugins
export ZSH_DIR=$HOME/dotfiles/zsh
source $ZSH_DIR/checks.zsh
source $ZSH_DIR/functions.zsh
source $ZSH_DIR/aliases.zsh
source $ZSH_DIR/completions.zsh
fpath=($ZSH_DIR/zsh-completions/src $fpath)
fpath=(/usr/local/share/zsh/site-functions $fpath)
source $ZSH_DIR/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
ZSH_HIGHLIGHT_HIGHLIGHTERS=(main brackets pattern)
source $ZSH_DIR/zsh-history-substring-search/zsh-history-substring-search.zsh
source $ZSH_DIR/opp/opp.zsh
source $ZSH_DIR/opp/opp/*.zsh

autoload -U compinit promptinit colors
compinit
promptinit
colors
grmlcomp
setopt prompt_subst extended_glob longlistjobs nonomatch notify completeinword auto_cd auto_pushd pushdignoredups

# History
HISTCONTROL=erasedups
HISTFILE=~/.zsh_history
HISTSIZE=10000
SAVEHIST=10000
setopt  append_history share_history hist_verify extended_history histignorespace hist_ignore_all_dups
export HISTIGNORE="&:ls:ll:la:l.:pwd:exit:clear:clr:c:x:[bf]g"

# Vi mode
bindkey -v
autoload -Uz edit-command-line
bindkey -M vicmd 'v' edit-command-line
bindkey -M vicmd '.' insert-last-word
export KEYTIMEOUT=1

# zsh-history-substring-search
zmodload zsh/terminfo
bindkey "$terminfo[kcuu1]" history-substring-search-up
bindkey "$terminfo[kcud1]" history-substring-search-down
bindkey -M vicmd 'k' history-substring-search-up
bindkey -M vicmd 'j' history-substring-search-down

# Change cursor shape according to vi mode in iTerm 2
# | = insert mode
# â–Œ = normal mode
if [[ $IS_OSX -eq 1 ]]; then
    function zle-keymap-select zle-line-init
    {
        case $KEYMAP in
            vicmd)      print -n -- "\E]50;CursorShape=0\C-G";;  # block cursor
            viins|main) print -n -- "\E]50;CursorShape=1\C-G";;  # line cursor
        esac
    
        zle reset-prompt
        zle -R
    }
    
    function zle-line-finish
    {
        print -n -- "\E]50;CursorShape=0\C-G"
    }
    
    zle -N zle-line-init
    zle -N zle-line-finish
    zle -N zle-keymap-select
fi

# dirs -v
DIRSTACKSIZE=${DIRSTACKSIZE:-20}
DIRSTACKFILE=${DIRSTACKFILE:-${ZDOTDIR:-${HOME}}/.zsh_directory_history}

if [[ -f ${DIRSTACKFILE} ]] && [[ ${#dirstack[*]} -eq 0 ]] ; then
    dirstack=( ${(f)"$(< $DIRSTACKFILE)"} )
    [[ -d $dirstack[1] ]] && cd $dirstack[1] && cd $OLDPWD
fi

chpwd() {
    if (( $DIRSTACKSIZE <= 0 )) || [[ -z $DIRSTACKFILE ]]; then return; fi
    local -ax my_stack
    my_stack=( ${PWD} ${dirstack} )
    builtin print -l ${(u)my_stack} >! ${DIRSTACKFILE}
}

# RVM and Virtualenv
if [ `hostname` = "mbp" ]; then
    source /usr/local/bin/virtualenvwrapper.sh
    PATH=$PATH:$HOME/.rvm/bin # Add RVM to PATH for scripting
    [[ -s $HOME/.rvm/scripts/rvm ]] && source $HOME/.rvm/scripts/rvm
fi

source $ZSH_DIR/prompt.zsh

# pass(1) 
export PASSWORD_STORE_DIR=$HOME/Dropbox/Passwords
